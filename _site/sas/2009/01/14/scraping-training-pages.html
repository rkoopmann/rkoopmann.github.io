<!DOCTYPE html>
<html>
  <head>
  <meta charset="utf-8">
  <meta http-equiv="X-UA-Compatible" content="IE=edge">
  <meta name="viewport" content="width=device-width, initial-scale=1">

  <title>scraping the sas training pages</title>
  <meta name="description" content="i thought it might be fun to see if i couldn’t pull the sas training catalog and corresponding schedules into a sas data set. easy enough, right?">

  <!-- Google Fonts loaded here depending on setting in _data/options.yml true loads font, blank does not-->
  
  
  <!-- Load up MathJax script if needed ... specify in /_data/options.yml file-->
  

  <link rel="stylesheet" type="text/css" href="/css/tufte.css">
  <!-- <link rel="stylesheet" type="text/css" href="/css/print.css" media="print"> -->

  <link rel="canonical" href="http://localhost:4000/sas/2009/01/14/scraping-training-pages.html">

  <link rel="alternate" type="application/rss+xml" title="hi" href="http://localhost:4000/feed.xml" />
</head>

  <body>
    <!--- Header and nav template site-wide -->
<header>
    <nav class="group">
	<a href="/"><img class="badge" src="/assets/img/badge_1.png" alt="CH"></a>
	
		
		    
		      <a href="/about/">About</a>
		    
	    
  	
		
		    
		      <a href="/sas/feed.json"></a>
		    
	    
  	
		
		    
		      <a href="/"></a>
		    
	    
  	
		
		    
		      <a href="/css/print.css"></a>
		    
	    
  	
		
  	
		
  	
		
		    
		      <a href="/assets/main.css"></a>
		    
	    
  	
		
		    
		      <a href="/feed.xml"></a>
		    
	    
  	
	</nav>
</header>

    <article class="group">
      <h1>scraping the sas training pages</h1>
<p class="subtitle">January 14, 2009</p>

<p>i thought it might be fun to see if i couldn’t pull the sas training catalog and corresponding schedules into a sas data set. easy enough, right?</p>

<!--more-->

<p><strong>Hello <a href="http://www.sas.com/news/newsletter/tech/2009_4.html">SAS Tech Report</a> readers!</strong> <em>be sure to check out the rest of <a href="/">this blog</a> and <a href="http://github.com/rkoopmann/sas-quatch">accompanying project</a>. you can <a href="/sas/feed.xml">follow this blog via your rss reader</a> and <a href="http://twitter.com/rkoopmann">follow me on twitter</a>. now back to the post…</em></p>

<p><strong>UPDATE:</strong> <em>it turns out that SAS has changed some things on their training site that affects the parsing of individual course schedules. they’ve apparently eliminated the directory for the print versions of the schedules. i’ll look into modifying the code when i find some free time. now back to the post…</em></p>

<p>sas puts this information online in two places: the <a href="http://support.sas.com/training/us/crs/">course listing</a> and individual course outline pages (for example, <a href="http://support.sas.com/training/us/crs/pr/rpt1.html">SAS Report Writing 1: Using Procedures and ODS</a>).</p>

<p>first step: get the catalog.</p>

<div class="highlighter-rouge"><pre class="highlight"><code>filename Courses url "http://support.sas.com/training/us/crs/" lrecl=1000;

data SAS_Catalog(drop=re_: course_flags i);
    infile Courses scanover;
    format Course_Code $10. Course_Desc $200. Schedule New SAS8 SAS9 LW BKS SGF 4.;
    retain re_course re_bks re_new re_s9 re_s8 re_lw re_sgf re_schedule Schedule--sgf;
    if _n_=1 then do;
        re_course=prxparse('/&lt;td class="newstext"&gt;&lt;a href="\/training\/us\/crs\/(.*).html"&gt;(.*)&lt;\/a&gt;(.*)&lt;\/td&gt;/');
        re_new = prxparse('/&lt;img src="\/training\/images\/new2.gif" width=44 height=14 alt="New" border=0&gt;/');
        re_s8  = prxparse('/&lt;span class="colorit"&gt;V8&lt;\/span&gt;/');
        re_s9  = prxparse('/&lt;img src="\/training\/images\/sas9sm.gif" width=26 height=14 alt="SAS 9" border=0&gt;/');
        re_lw  = prxparse('/&lt;span class="lw"&gt;&lt;nobr&gt;Live Web&lt;\/nobr&gt;&lt;\/span&gt;/');
        re_bks = prxparse('/&lt;span class="bks"&gt;BKS&lt;\/span&gt;/');
        re_sgf = prxparse('/&lt;img src="\/training\/images\/sgf_icon.gif" width=100 height=14 border=0 alt="SAS Global Forum"&gt;/');

        re_schedule=prxparse('/&lt;td class="newstext"&gt;&lt;a href="http.*\?course_code=(.*)&amp;ctry=us"&gt;schedule&lt;\/a&gt;.*&lt;\/td&gt;/');
    end;
    input;
    if prxmatch(re_course, _infile_) then do;
        course_code = prxposn(re_course, 1, _infile_);
        course_desc = prxposn(re_course, 2, _infile_);
        course_flags= prxposn(re_course, 3, _infile_);
        if course_flags ne ' ' then do;
            New = (prxmatch(re_new, course_flags) ne 0);
            SAS8= (prxmatch(re_s8 , course_flags) ne 0);
            SAS9= (prxmatch(re_s9 , course_flags) ne 0);
            LW  = (prxmatch(re_lw , course_flags) ne 0);
            BKS = (prxmatch(re_bks, course_flags) ne 0);
            SGF = (prxmatch(re_sgf, course_flags) ne 0);
            if sum(of new--sgf) le 0 then put course_code;
        end;
    end;
    else if prxmatch(re_schedule, _infile_) then do;
        course_code = prxposn(re_schedule, 1, _infile_);
        Schedule = 1;
        return;
    end;
    else do;
        array t(*) Schedule New SAS8 SAS9 LW BKS SGF;
        do i = 1 to dim(t);
            t[i] = 0;
        end;
        delete;
    end;
    output;
run;

filename Courses clear;

proc sort data=SAS_Catalog; by course_code; run;
</code></pre>
</div>

<p>step two: set up a macro for processing individual course outline pages.</p>

<div class="highlighter-rouge"><pre class="highlight"><code>%macro getSchedule(COURSE_CODE);
filename schedule url "http://support.sas.com/training/us/crs/pr/&amp;COURSE_CODE..html" lrecl=1500;
data _schedule_ (drop=re_: offer1-offer3 duration_course duration_liveweb);
  format Course_Code $10. Location $200. Start date9. Duration CEU 4.1 offer1-offer3 $1000.;
  infile schedule scanover;
  retain Course_Code re_offering re_duration re_br location start duration_course duration_liveweb ceu;
  if _n_ = 1 then do;
      course_code = "&amp;COURSE_CODE";
      re_duration=prxparse('/&lt;p class="newstext"&gt;((Classroom )?[Dd]uration: (\d\.\d|\d) day(s)? )?( )*((Live Web d|D)uration: (\d\.\d|\d) half-day session(s)? )?( )*CEU: (\d\.\d|\d) &lt;\/p&gt;/');
      re_offering=prxparse('/&lt;tr valign="top"&gt;&lt;td class="newstext"&gt;(.*)&lt;\/td&gt;&lt;td class="newstext"&gt;(.*)&lt;\/td&gt;&lt;td class="newstext"&gt;(.*)&lt;\/td&gt;&lt;\/tr&gt;/');
      re_br      =prxparse('/(\d\d\w\w\w\d\d\d\d)\s+([A-Za-z,\.\s]*)&lt;br&gt;(.*)/');
      end;
  input;
  if prxmatch(re_duration, _infile_) then do;
      duration_course   = input(prxposn(re_duration, 3, _infile_), 4.);
      duration_liveweb  = input(prxposn(re_duration, 8, _infile_), 4.);
      ceu               = input(prxposn(re_duration, 11, _infile_), 4.);
      return;
      end;
  if prxmatch(re_offering, _infile_) then do;
      offer1 = prxposn(re_offering, 1, _infile_);
      do while (prxmatch(re_br, offer1));
          Start   =input(prxposn(re_br, 1, offer1), date9.);
          Location=prxposn(re_br, 2, offer1);
          if location eq 'Live Web,' then duration = duration_liveweb;
                                     else duration = duration_course;
          offer1  =prxposn(re_br, 3, offer1);
          output;
          end;
      offer2 = prxposn(re_offering, 2, _infile_);
      do while (prxmatch(re_br, offer2));
          Start   =input(prxposn(re_br, 1, offer2), date9.);
          Location=prxposn(re_br, 2, offer2);
          if location eq 'Live Web,' then duration = duration_liveweb;
                                     else duration = duration_course;
          offer2  =prxposn(re_br, 3, offer2);
          output;
          end;
      offer3 = prxposn(re_offering, 3, _infile_);
      do while (prxmatch(re_br, offer3));
          Start   =input(prxposn(re_br, 1, offer3), date9.);
          Location=prxposn(re_br, 2, offer3);
          if location eq 'Live Web,' then duration = duration_liveweb;
                                     else duration = duration_course;
          offer3  =prxposn(re_br, 3, offer3);
          output;
          end;
      end;
  else delete;
run;
filename schedule clear;
proc append base=SAS_Schedule data=_schedule_ force; run;
proc datasets library=work nodetails nolist; delete _schedule_; run; quit;
%mend;
</code></pre>
</div>

<p>finally, cycle through each course that has a schedule available.</p>

<div class="highlighter-rouge"><pre class="highlight"><code>proc datasets library=work nodetails nolist;
  delete sas_schedule;
  run;
quit;
data _null_;
  set SAS_Catalog;
  where schedule;
  calltext = cats('%getSchedule(', course_code, ');');
  call execute(calltext);
run;

proc sql;
  create table SAS_Training as
  select distinct c.*, s.*
  from sas_catalog as c
  left join sas_schedule as s
    on c.course_code=s.course_code
  order by course_desc, location, start
  ;
quit;
</code></pre>
</div>

<p>see? it was easy. oh, the file can be downloaded <a href="http://sas-quatch.googlecode.com/files/sas%20training.sas">here</a>.</p>




    </article>
    <span class="print-footer">scraping the sas training pages - January 14, 2009 - </span>


    <footer>
  <hr class="slender">
  <ul class="footer-links">
    <li><a href="mailto:hate@spam.net"><span class="icon-mail"></span></a></li>    
      
  </ul>
<div class="credits">
<span>&copy; 2017 &nbsp;&nbsp;</span></br> <br>
<span>This site created with the <a href="//github.com/clayh53/tufte-jekyll">Tufte theme for  </a> in <a href="//jekyllrb.com">Jekyll</a>.</span> 
</div>  
</footer>

  </body>
</html>
